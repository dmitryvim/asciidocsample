
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.3.RELEASE"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.+"
    }
}

group "com.bg"
version "1.0-SNAPSHOT"

apply plugin: "groovy"
apply plugin: "java"
apply plugin: "idea"
apply plugin: "org.asciidoctor.convert"
apply plugin: "org.springframework.boot"


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

ext["spock.version"] = "1.1-groovy-2.4-rc-4"

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:1.5.3.RELEASE"
    }
}

dependencies {
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor:1.2.0.RELEASE'

    compile "org.projectlombok:lombok:1.16.+"
    compile "org.springframework.boot:spring-boot-devtools"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.restdocs:spring-restdocs-mockmvc"

    testCompile "org.codehaus.groovy:groovy-all"
    testCompile "org.spockframework:spock-spring"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.springframework.restdocs:spring-restdocs-mockmvc"
}

ext["snippetsDir"] = file('build/generated-snippets')

test {
    outputs.dir snippetsDir
}

asciidoctorj {
    version = '1.5.4'
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}


